class CT_Lazer : MWeapon
{
	Default
	{
		//$Category "Weapons"
		//$Title "Lazer Cannon"
		Weapon.AmmoUse 1;
		Weapon.AmmoGive 5;
		Weapon.AmmoType "CT_PowerPack";
		Inventory.PickupMessage "Lazer Cannon";
		Obituary "$OB_MPSHOTGUN";//Do this at some point
	}
	States
	{
	Ready:
		VCAN AAAABCDEGHIJKLMNOP 2 CT_WeaponReady;
		Loop;
	Deselect:
		VCAN A 1 CT_Lower;
		Loop;
	Select:
		VCAN A 1 CT_Raise;
		Loop;
	Fire:
		VCAN AQRQRQR 2 CT_ResetOffset();
	
	
		//VCAN JKJK 2 CT_ResetOffset();
		VCAN S 4
			{
				A_AlertMonsters();
				A_FireProjectile("LazerShot");
				CT_ResetOffset();
			}
		VCAN T 3;
		VCA2 A 3;
		VCA2 B 4;
		VCA2 CDEFG 2;
		VCAN A 2;
		VCA2 H 2;
		VCAN A 2;
		VCNN ABACADAEAFAGAHAI 1;
		VCAN A 1 A_ReFire("Fire");
		VCAN A 0 CT_ResetAngleOffsets();
		Goto Ready;
	Spawn:
		VCAN Z -1;
		Stop;
	}
}

class LazerSmoke : Actor
{
	Default
	{
	+NOBLOCKMAP	+NOGRAVITY	+NOINTERACTION	+ROLLSPRITE
	//RenderStyle "Stencil";
	//StencilColor "FFFFFF";
	}
	States
	{
	Spawn:
		VLAS AABCDE 2;
		Stop;
	}
	override void PostBeginPlay()
	{	
		int offset = 2;
		A_Warp(AAPTR_DEFAULT, 0, frandom(-1 * offset,offset), frandom(-1 * offset,offset), 0, WARPF_NOCHECKPOSITION);
		A_SetRoll(random(0,365));
		if(random(0,3) <= 0)
			A_SpawnItemEX("LazerSmoke2");
	}
}

class LazerSmoke2 : LazerSmoke
{
	States
	{
	Spawn:
		VLAS M 3 Bright;
		Stop;
	}
	override void PostBeginPlay()
	{	
		int offset = 2;
		A_SetScale(frandom(0.8, 1.2));
		A_SetRoll(random(0,365));
		frame += random(0,3);
	}
}

class LazerShot : FastProjectile
{
	Default
	{
		Radius 6;
		Height 8;
		Speed 75;
		DamageFunction (20);//never randomizing damage again!
		Projectile;
		+BRIGHT
		+ROLLSPRITE
		+BLOODSPLATTER
		SeeSound "imp/attack";
		MissileHeight 8;//why does this spawn 8 units low by default?
		MissileType "LazerSmoke";
	}
	States
	{
	Spawn:
		VLAS A 1;
		Loop;
	Death:
		TNT1 A 0 A_SetRoll(-45);
		VLAS FGFG 2;
		VLAS H 4 A_Explode(60, 96, XF_HURTSOURCE, TRUE);
		VLAS IJKL 3;
		Stop;
	}
}